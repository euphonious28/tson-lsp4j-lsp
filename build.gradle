plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
}

group 'org.euph28'
version '1.2.0'
def mainClassName = 'com.tson.lsp.Launcher'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/euphonious28/tson")
        credentials {
            username = project.findProperty("gitHubUsername") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gitHubToken") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

compileJava{
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.12.0'
    implementation 'com.euph28:tson:1.1.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/euphonious28/tson-lsp4j-lsp"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}